// ----------------------------
// Lark grammar for RomanText
// ----------------------------
?start: document

document: (NEWLINE)* line*
line: metadata_line
     | statement_line

// ----------------------------
// Metadata Rules
// ----------------------------
metadata_line: composer_line
             | piece_line
             | analyst_line
             | proofreader_line
             | movement_line
             | time_signature_line
             | key_signature_line
             | minor_mode_line
             | tempo_line
             | instrument_line

composer_line: "Composer:" WS REST_LINE NEWLINE
piece_line: "Piece:" WS REST_LINE NEWLINE
analyst_line: "Analyst:" WS REST_LINE NEWLINE
proofreader_line: "Proofreader:" WS REST_LINE NEWLINE
movement_line: "Movement:" WS MEASURE_NUMBER NEWLINE
time_signature_line: "Time Signature:" WS time_signature NEWLINE
tempo_line: "Tempo:" WS* TEMPO_NUMBER NEWLINE  // Tempo number in QPM
key_signature_line: "Key Signature:" WS SIGNED_INT NEWLINE
minor_mode_line: "Minor Sixth / Minor Seventh:" WS MINOR_MODE_OPTION NEWLINE
instrument_line: "Instrument:" WS VOICE_NAME WS* "=" WS* GM_INSTRUMENT_NAME ("," WS* VOICE_NAME WS* "=" WS* GM_INSTRUMENT_NAME)* NEWLINE

VOICE_NAME: "V" DIGIT+
GM_INSTRUMENT_NAME: /[a-zA-Z_]+/
TEMPO_NUMBER: DIGIT+

// ----------------------------
// Statement Rules
// ----------------------------
statement_line: measure_line
              | pedal_line
              | form_line
              | note_line
              | repeat_line
              | melody_line
              | accompaniment_line
              | event_line
              | variable_declaration_line
              | technique_line


// --- Technique line
technique_line: "tech" WS (voice_list_for_technique | single_voice_for_technique) WS+ measure_range_with_beats WS* ":" WS* technique_list NEWLINE
voice_list_for_technique: "[" WS* VOICE_NAME ("," WS* VOICE_NAME)* WS* "]"
single_voice_for_technique: VOICE_NAME
measure_range_with_beats: "(" MEASURE_INDICATOR WS+ BEAT_INDICATOR WS+ "-" WS MEASURE_INDICATOR WS+ BEAT_INDICATOR ")"
technique_list: TECHNIQUE_NAME ("," WS* TECHNIQUE_NAME)*
TECHNIQUE_NAME: /[a-zA-Z_][a-zA-Z0-9_]*/

// --- Variable declaration line
variable_declaration_line: VARIABLE_NAME WS* "=" WS* variable_content NEWLINE
variable_content: (accompaniment_line_content | melody_line_content | measure_line_content)
VARIABLE_NAME: /[_a-zA-Z][_a-zA-Z0-9]*/
VARIABLE_VALUE: REST_LINE
VARIABLE_CALLING: "@" VARIABLE_NAME

// --- Event Lines
event_line: "e" MEASURE_NUMBER WS* event_content+ NEWLINE
event_content: BEAT_INDICATOR WS* EVENT_FUNCTION_NAME + "(" + EVENT_ARGUMENT + ")"
EVENT_FUNCTION_NAME: "tempo" | "velocity" | "start_crescendo" | "end_crescendo" | "start_diminuendo" | "end_diminuendo" | "start_accelerando" | "end_accelerando" | "start_ritardando" | "end_ritardando"
EVENT_ARGUMENT: TEMPO_NUMBER | VELOCITY_VALUE
VELOCITY_VALUE: "pppp" | "ppp" | "pp" | "p" | "mp" | "mf" | "f" | "ff" | "fff" | "ffff"

// --- Measure / Harmonic Lines
measure_line: MEASURE_INDICATOR (measure_line_content|VARIABLE_CALLING) NEWLINE
measure_line_content: (chord_beat_1)? (beat_chord | key_change)* PHRASE_BOUNDARY?
chord_beat_1: (WS key)? WS chord
beat_chord: WS BEAT_INDICATOR (WS key)? WS chord
key_change: WS key
key: KEY

repeat_line: measure_range WS "=" WS measure_range NEWLINE
measure_range: MEASURE_INDICATOR ("-" MEASURE_INDICATOR)+

note: NOTELETTER ACCIDENTAL?
NOTELETTER: /[A-Ga-g]/

// --- Pedal Lines
pedal_line: "Pedal:" WS note WS pedal_entries NEWLINE
pedal_entries: pedal_entry (WS pedal_entry)* WS?
pedal_entry: MEASURE_INDICATOR WS BEAT_INDICATOR

// --- Form and Note Lines
form_line: "Form:" WS REST_LINE NEWLINE
note_line: "Note:" WS REST_LINE NEWLINE

// ----------------------------
// Chord Grammar
// ----------------------------
chord: ( chord_component ( "/" tonality_component )* )
chord_component: ( special_chord | standard_chord ) chord_alteration*
tonality_component: [chord_accidental] numeral
special_chord: SPECIAL_CHORD [ inversion ]
standard_chord: [ chord_accidental ] numeral [ CHORD_QUALITY ] [ inversion ]
numeral: ROMAN_NUMERAL
chord_accidental: ACCIDENTAL
CHORD_QUALITY: "o" | "+" | "%" | "ø" | "°"
chord_alteration: "[" alteration_content "]"
omit_alteration: "no"
add_alteration: "add"
alteration_content: (( omit_alteration | add_alteration )? [ ACCIDENTAL ] DIGITS)

// ----------------------------
// Accompaniment Grammar
// ----------------------------
accompaniment_line: ACCOMPANIMENT_INDICATOR WS+ (VOICE_NAME)? (accompaniment_line_content | VARIABLE_CALLING) NEWLINE
accompaniment_line_content: (voice_list)? (BEAT_INDICATOR WS+ (voice_list | SILENCE))* 
ACCOMPANIMENT_INDICATOR: "acc" DIGIT+
voice_list: (ALTERATION? VOICE OCTAVE?)+
VOICE: /[1-4]/
// Set the octave one octave higher or lower (o2=2 octaves up, o-2=2 octaves down)
OCTAVE: "o" SIGNED_INT
// Set the note one semitone higher or lower (++=2 semitones up, --=2 semitones down)
ALTERATION: ("+" | "-")+

// ----------------------------
// Melody Grammar
// ----------------------------
melody_line: MELODY_MEASURE_INDICATOR (WS+ VOICE_NAME)? (melody_line_content | VARIABLE_CALLING) NEWLINE
melody_line_content: (first_beat_note)? (beat_note)*
MELODY_MEASURE_INDICATOR: "mel" MEASURE_NUMBER
MELODY_BEAT_INDICATOR: ("b" | "t") DIGIT+ ("." DIGIT+)?
first_beat_note: WS+ (MELODY_BEAT_INDICATOR WS+)? MELODY_NOTE (DELTA_OCTAVE)?
beat_note: WS+ MELODY_BEAT_INDICATOR WS (MELODY_NOTE (DELTA_OCTAVE)? | ABSOLUTE_NOTE | SILENCE)
SILENCE: "r" | "R"
MELODY_NOTE: "/" (ACCIDENTAL)? DIGIT+
DELTA_OCTAVE: "o" SIGNED_INT
ABSOLUTE_NOTE: NOTELETTER_CAPITALIZED (ACCIDENTAL)? (ABSOLUTE_OCTAVE)?
NOTELETTER_CAPITALIZED: /[A-G]/
ABSOLUTE_OCTAVE: DIGIT+

// ----------------------------
// Tokens & Basic Patterns
// ----------------------------
MEASURE_INDICATOR: "m" MEASURE_NUMBER
MEASURE_NUMBER: DIGIT+ (LETTER+ | "var" DIGIT+)? 
BEAT_INDICATOR: ("b" | "t") BEAT_NUMBER
BEAT_NUMBER: DIGIT+ ("." DIGIT+)?

time_signature: numerator "/" denominator
numerator: DIGIT+
denominator: DIGIT+
SIGNED_INT: ("+"|"-")? DIGIT+

KEY: /[A-Ga-g](#{1,}|b{1,})?:/
SPECIAL_CHORD: "Ger" | "It" | "Fr" | "N" | "Cad" | "NC" | "R" | "r"
ROMAN_NUMERAL: "I" | "II" | "III" | "IV" | "V" | "VI" | "VII"
             | "i" | "ii" | "iii" | "iv" | "v" | "vi" | "vii"
ACCIDENTAL: /#{1,}|b{1,}/

inversion: INVERSION_STANDARD | inversion_free
INVERSION_STANDARD: "6" | "64" | "6/3" | "6/4" | "7" | "6/5" | "65" | "4/3" | "43" | "2" | "42" | "4/2" | "9" | "11" | "13"
inversion_free: ACCIDENTAL? DIGIT+

MINOR_MODE_OPTION: "quality" | "cautionary" | "sharp" | "flat"

DIGIT: /[0-9]/
DIGITS: DIGIT+
LETTER: /[a-z]+/

REST_LINE: /.+/
PHRASE_BOUNDARY: WS "||" WS?

WS: (" " | /\t/)+
CR: /\r/
LF: /\n/
NEWLINE: WS* (CR? LF)+

%ignore WS
%ignore NEWLINE
%ignore CR
%ignore LF
%ignore PHRASE_BOUNDARY