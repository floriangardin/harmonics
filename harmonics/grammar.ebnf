?start: document

// ======== TERMINAL DEFINITIONS ========

TRACK_NAME: "T" DIGIT+
VOICE_NAME: "v" DIGIT+
COMPLEX_BEAT: DIGIT+  "+" DIGIT+ "/" DIGIT+
GM_INSTRUMENT_NAME: /[a-zA-Z_1-9]+/
GROUP_NAME: /[a-zA-Z_][a-zA-Z0-9_]+/
TEMPO_NUMBER: DIGIT+
TECHNIQUE_NAME: STOP_TECHNIQUE? (PLAYING_STYLE | /[a-zA-Z_][a-zA-Z0-9_]*/)
STOP_TECHNIQUE: "!" 
EVENT_FUNCTION_NAME: "tempo" | "velocity" | "start_crescendo" | "end_crescendo" | "start_diminuendo" | "end_diminuendo" | "start_accelerando" | "end_accelerando" | "start_ritardando" | "end_ritardando"
EVENT_ARGUMENT: TEMPO_NUMBER | VELOCITY_VALUE
VELOCITY_VALUE: "ffff" | "fff" | "ff" | "f" | "mf" | "mp" | "p" | "pp" | "ppp" | "pppp"
NOTELETTER: /[A-Ga-g]/
CHORD_QUALITY: "°" | "ø" | "%" | "+" | "o"
VOICE: /[1-4]/
OCTAVE: "o" SIGNED_INT 
ALTERATION: ("+" | "-")+
SILENCE: (END_PEDAL)? ("R" | "r") (START_PEDAL)?
CONTINUATION: (END_PEDAL)? ("L" | "l") (START_PEDAL)?
NOTELETTER_CAPITALIZED: /[A-G]/
ABSOLUTE_OCTAVE: DIGIT+
PLAYING_STYLE: STACCATISSIMO | STACCATO | MARCATO | ACCENT | TENUTO | START_TIE | TREMOLO | TURN | INVERTED_TURN | MORDENT | INVERTED_MORDENT | START_PEDAL
STACCATO: "."
STACCATISSIMO: "!"
TENUTO: "-"
MARCATO: "^"
ACCENT: ">"
START_TIE: "_"
END_PLAYING_STYLE: END_TIE | END_PEDAL
END_TIE: "_"
START_PEDAL: "*"
END_PEDAL: "*"
TREMOLO: "tr"
TURN: "~"
INVERTED_TURN: "i~"
MORDENT: "/~"
INVERTED_MORDENT: "i/~"
MEASURE_INDICATOR: "m" MEASURE_NUMBER
MEASURE_NUMBER: DIGIT+ (LETTER+ | "var" DIGIT+)? 
BEAT_INDICATOR: "b" BEAT_NUMBER
BEAT_NUMBER: DIGIT+ ("." DIGIT+)? | COMPLEX_BEAT
SIGNED_INT: ("+"|"-")? DIGIT+
KEY: /[A-Ga-g](#{1,}|b{1,})?:/
KEY_NAME: /[A-Ga-g](#{1,}|b{1,})?/
ROMAN_NUMERAL: "Ger" | "Fr" | "It" | "Cad" | "NC" | "N" | "R" | "r" |"VII" | "III" | "IV" | "VI" | "II" | "V" | "It" | "I"
             | "vii" | "iii" | "iv" | "vi" | "ii" | "v" | "i"
ACCIDENTAL: "##" | "bb" | "b" | "#"
ACCIDENTAL_WITH_NUMERAL: ACCIDENTAL ROMAN_NUMERAL
INVERSION_STANDARD: "6/4" | "6/3" | "65" | "6/5" | "4/3" | "4/2" | "42" | "43" | "64" | "13" | "11" | "9" | "7" | "6" | "2"
DIGIT: /[0-9]/
DIGITS: DIGIT+
LETTER: /[a-z]+/
REST_LINE: /.+/
PHRASE_BOUNDARY: "||"
CLEF_NAME: "mezzo-soprano" | "treble8vb" | "bass8vb" | "treble" | "soprano" | "baritone" | "sub-bass" | "french" | "tenor" | "alto" | "bass" | "G" | "F" | "C"
CLEF_OCTAVE_CHANGE: ("+" | "-") DIGIT+
TEXT_COMMENT: "\"" /[^"]+/ "\""

// ======== NON-TERMINAL RULES ========

document: (line NEWLINE?)*
line: composer_line
             | title_line
             | time_signature_line
             | tempo_line
             | instrument_line
             | clef_line
             | groups_line
             | harmony_line
             | COMMENT
             | melody_line
             | event_line
             | technique_line
             | clef_change_line
             | key_signature_line
             
composer_line: "Composer:" REST_LINE
title_line: "Piece:" REST_LINE
time_signature_line: ( "(" "m" MEASURE_NUMBER ")" )? "Time Signature" ":" time_signature
tempo_line: "Tempo:" TEMPO_NUMBER  // Tempo number in QPM
instrument_line: "Instrument:" TRACK_NAME "=" GM_INSTRUMENT_NAME ("," TRACK_NAME "=" GM_INSTRUMENT_NAME)*
clef_line: ( "(" "m" MEASURE_NUMBER ")" )? "Clef:" TRACK_NAME "=" clef_type
key_signature_line: ( "(" "m" MEASURE_NUMBER ")" )? "Signature:" key_signature
// To group tracks together (For example for a piano score)
groups_line: "Groups:" GROUP_NAME "=" "[" TRACK_NAME ("," TRACK_NAME)* "]"
key_signature: ACCIDENTAL*

technique_line: "tech" (voice_list_for_technique | single_voice_for_technique) measure_range_with_beats ":" technique_list
voice_list_for_technique: "[" TRACK_NAME ("," TRACK_NAME)* "]"
single_voice_for_technique: TRACK_NAME
measure_range_with_beats: "(" MEASURE_INDICATOR BEAT_INDICATOR "-" MEASURE_INDICATOR BEAT_INDICATOR ")"
technique_list: TECHNIQUE_NAME ("," TECHNIQUE_NAME)*

event_line: "e" MEASURE_NUMBER event_content+
event_content: BEAT_INDICATOR EVENT_FUNCTION_NAME "(" EVENT_ARGUMENT ")"

harmony_line: "h" MEASURE_NUMBER harmony_line_content
harmony_line_content: (beat_chord | key_change)+ PHRASE_BOUNDARY?
beat_chord: BEAT_INDICATOR key? chord
key_change: key
key: KEY

note: NOTELETTER ACCIDENTAL?

COMMENT: "//" REST_LINE NEWLINE

melody_line: MEASURE_INDICATOR (TRACK_NAME ("." VOICE_NAME)? )? melody_line_content
melody_line_content: beat_note+
beat_note: BEAT_INDICATOR TEXT_COMMENT? (absolute_note+ | SILENCE | CONTINUATION) note_techniques?
note_techniques: "[" TECHNIQUE_NAME ("," TECHNIQUE_NAME)* "]"
absolute_note: END_PLAYING_STYLE? NOTELETTER_CAPITALIZED ACCIDENTAL? ABSOLUTE_OCTAVE PLAYING_STYLE*

chord: chord_component ( "/" tonality_component )*
chord_component: standard_chord chord_alteration*
tonality_component: ACCIDENTAL_WITH_NUMERAL | ROMAN_NUMERAL | KEY_NAME
standard_chord: ACCIDENTAL? ROMAN_NUMERAL CHORD_QUALITY? inversion?
chord_alteration: "[" alteration_content "]"
omit_alteration: "no"
add_alteration: "add"
alteration_content: (omit_alteration | add_alteration)? ACCIDENTAL? DIGITS

time_signature: numerator "/" denominator
numerator: DIGIT+
denominator: DIGIT+

inversion: INVERSION_STANDARD | inversion_free
inversion_free: ACCIDENTAL? DIGIT+

clef_change_line: MEASURE_INDICATOR TRACK_NAME? BEAT_INDICATOR "clef" clef_type
clef_type: CLEF_NAME CLEF_OCTAVE_CHANGE?

%import common.NEWLINE
%import common.WS

%ignore WS
%ignore COMMENT